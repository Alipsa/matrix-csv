plugins {
  id 'groovy'
  id 'java-library'
  id 'signing'
  id 'maven-publish'
  id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
  id "com.github.ben-manes.versions" version "0.46.0"
}

group 'se.alipsa.groovy'
version '1.0.1-SNAPSHOT'
description = "Groovy library for importing structured text into a matrix"


java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  def groovyVersion = '4.0.12'
  def groovy = "org.apache.groovy:groovy:$groovyVersion"
  compileOnly 'se.alipsa.groovy:matrix:1.1.1'
  api 'org.apache.commons:commons-csv:1.10.0'
  compileOnly groovy
  testImplementation groovy
  testImplementation 'se.alipsa.groovy:matrix:1.1.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
  testImplementation 'se.alipsa.groovy:matrix-datasets:1.0.1'
}

test {
  testLogging.showStandardStreams = true
  useJUnitPlatform()
}

//Maven Central uploads
task javadocJar(type: Jar, dependsOn: groovydoc) {
  archiveClassifier.set('javadoc')
  from groovydoc.destinationDir
}


task sourcesJar(type: Jar, dependsOn: classes) {
  archiveClassifier.set('sources')
  from sourceSets.main.allSource
}

nexusPublishing {
  repositories {
    sonatype()
  }
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
      artifact(javadocJar)
      artifact(sourcesJar)
      pom {
        name = 'Groovy Matrix CSV'
        description = "${project.description}"
        url = "https://github.com/Alipsa/matrix-csv"
        licenses {
          license {
            name = 'MIT License'
            url = 'https://raw.githubusercontent.com/Alipsa/matrix-csv/master/LICENSE'
          }
        }
        developers {
          developer {
            id = 'perNyfelt'
            name = 'Per Nyfelt'
          }
        }
        scm {
          url = 'https://github.com/Alipsa/matrix-csv/tree/master'
          connection = 'scm:git:https://github.com/Alipsa/matrix-csv.git'
          developerConnection = 'scm:git:https://github.com/Alipsa/matrix-csv.git'
        }
      }
    }
  }
}

signing {
  if (project.properties['signing.keyId'] != null) {
    project.logger.lifecycle("Signing artifacts...")
    sign publishing.publications.maven
  } else {
    project.logger.lifecycle("signing.keyId is not defined, skipping signing of artifacts...")
  }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}
// https://github.com/ben-manes/gradle-versions-plugin
tasks.named("dependencyUpdates").configure {
  resolutionStrategy {
    componentSelection {
      all {
        if (isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)) {
          reject('Release candidate')
        }
      }
    }
  }
}